plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.example.starbucknotetaker'
    compileSdk 34
    ndkVersion '26.1.10909125'

    defaultConfig {
        applicationId 'com.example.starbucknotetaker'
        minSdk 26
        targetSdk 34
        versionCode System.getenv("ANDROID_VERSION_CODE")?.toInteger() ?: 1
        versionName System.getenv("ANDROID_VERSION_NAME") ?: '1.0'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            def keystorePath = System.getenv("ANDROID_KEYSTORE_PATH")
            if (keystorePath) {
                def keyFile = keystorePath.startsWith("/") ? file(keystorePath) : rootProject.file(keystorePath)
                storeFile keyFile
                storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
                keyAlias System.getenv("ANDROID_KEY_ALIAS")
                keyPassword System.getenv("ANDROID_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            if (signingConfigs.release.storeFile != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
        }
        debug {
            if (signingConfigs.release.storeFile != null) {
                signingConfig signingConfigs.release
            }
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.10'
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        coreLibraryDesugaringEnabled true
    }

    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
        jniLibs {
            useLegacyPackaging true
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation platform('androidx.compose:compose-bom:2024.06.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.navigation:navigation-compose:2.7.7'
    implementation 'io.coil-kt:coil-compose:2.5.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'com.google.android.material:material:1.12.0' // Or the latest version
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation("org.tensorflow:tensorflow-lite:2.17.0") {
        exclude group: "org.tensorflow", module: "tensorflow-lite-api"
    }
    implementation("org.tensorflow:tensorflow-lite-support:0.4.4") {
        exclude group: "org.tensorflow", module: "tensorflow-lite-support-api"
        exclude group: "org.tensorflow", module: "tensorflow-lite-api"
    }
    implementation("org.tensorflow:tensorflow-lite-task-text:0.4.4") {
        exclude group: "org.tensorflow", module: "tensorflow-lite-support-api"
        exclude group: "org.tensorflow", module: "tensorflow-lite-api"
    }
    implementation 'org.tensorflow:tensorflow-lite-api:2.17.0'
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation 'ai.djl.sentencepiece:sentencepiece:0.33.0'
    runtimeOnly 'ai.djl.android:tokenizer-native:0.33.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'org.tensorflow:tensorflow-lite-api:2.17.0'
    testImplementation 'org.json:json:20240303'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2024.06.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
